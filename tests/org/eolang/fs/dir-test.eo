+package org.eolang.fs
+alias org.eolang.fs.dir
+alias org.eolang.fs.tmpdir
+alias org.eolang.io.copied
+alias org.eolang.io.bytes-as-input
+junit

[] > makes-new-directory
  tmpdir.tmpfile.@ > f!
  dir > d
    f.resolve "new-dir"
  and. > @
    f.rm.@
    d.mkdir
    d.exists
    d.is-dir

[] > makes-new-directory-on-top-of-file
  dir > d!
    tmpdir.tmpfile.@
  and. > @
    d.rm.@
    d.mkdir
    d.exists
    d.is-dir

[] > deletes-recursively
  dir > d!
    tmpdir.tmpfile.@
  and. > @
    d.rm.@
    d.mkdir
    eq.
      copied
        bytes-as-input
          "Hello, world".as-bytes
        as-output.
          d.resolve "test.txt"
          "w"
        1024
      12
    d.rm-rf
    d.exists.not

[] > walks-recursively
  dir > d!
    tmpdir.tmpfile.@
  and. > @
    d.rm.@
    d.mkdir
    mkdir.
      dir
        d.resolve "foo/bar"
    (d.resolve "foo/bar/test.txt").touch
    mkdir.
      dir
        d.resolve "x/y/z"
    (d.resolve "x/y/z/a.txt").touch
    eq.
      length.
        d.walk
          "**/*.txt"
      2
